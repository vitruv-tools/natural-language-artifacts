import "http://vitruv.tools/methodologisttemplate/families" as families
import "http://vitruv.tools/methodologisttemplate/persons" as persons

reactions: familiesToPersons
in reaction to changes in families
execute actions in persons

reaction CreatedFather {
	after element families::Member inserted in families::Family[father]
	call updateNameAndCorrespondencesOfCorrespondingPerson(newValue) createMaleFromNewMember(newValue)
}

routine updateNameAndCorrespondencesOfCorrespondingPerson(families::Member member) {
	match {
		val person = retrieve persons::Person corresponding to member
		val family = retrieve families::Family corresponding to member
	}
	update {
		person.fullName = member.firstName + " " + family.lastName
	}
}

routine createMaleFromNewMember(families::Member member) {
	match {
		require absence of persons::Person corresponding to member
	}
	update {
		createMale(member)
	}
}

routine createMale(families::Member member) {
	match {
		val family = retrieve families::Family corresponding to member
		val personRegister = retrieve persons::PersonRegister corresponding to family.eContainer
	}
	create {
		val newMale = new persons::Male
	}
	update {
		newMale.fullName = member.firstName + " " + family.lastName
		newMale.birthday = new java.util.Date()
		personRegister.persons += newMale
		addCorrespondenceBetween(newMale, member)
	}
}