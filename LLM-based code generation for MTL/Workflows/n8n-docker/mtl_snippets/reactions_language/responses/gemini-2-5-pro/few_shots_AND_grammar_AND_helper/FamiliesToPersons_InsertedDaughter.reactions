import "http://vitruv.tools/methodologisttemplate/families" as families
import "http://vitruv.tools/methodologisttemplate/persons" as persons

reactions: familiesToPersons
in reaction to changes in families
execute actions in persons

reaction InsertedDaughter {
	after element families::Member inserted in families::Family[daughters]
	call createOrFindFemale(newValue)
}

routine createOrFindFemale(families::Member daughter) {
	update {
		call createFemale(daughter)
		call updateNameAndCorrespondencesOfCorrespondingPerson(daughter)
	}
}

routine createFemale(families::Member daughter) {
	match {
		require absence of persons::Person corresponding to daughter
		val familyRegister = daughter.eContainer.eContainer as families::FamilyRegister
		val personRegister = retrieve persons::PersonRegister corresponding to familyRegister
	}
	create {
		val newFemale = new persons::Female
	}
	update {
		personRegister.persons.add(newFemale)
		addCorrespondenceBetween(daughter, newFemale)
	}
}

routine updateNameAndCorrespondencesOfCorrespondingPerson(families::Member daughter) {
	match {
		val family = daughter.familyDaughter
		val female = retrieve persons::Female corresponding to daughter
	}
	update {
		female.fullName = daughter.firstName + " " + family.lastName
	}
}